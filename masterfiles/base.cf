bundle common base_config
{
 vars:
	"net_hostname" string => "ocemr-server";
	"net_domain" string => "ocemr";
	"net_dev" string => "eth0";
	"net_addr" string => "192.168.7.2";
	"net_mask" string => "255.255.255.0";
	"net_gateway" string => "192.168.7.1";
	"net_dns1" string => "8.8.4.4";
	"net_dns2" string => "8.8.8.8";
	"net_dhcp_min" string => "192.168.7.101";
	"net_dhcp_max" string => "192.168.7.199";
}

bundle agent base
{

methods:
		"any" usebundle => base_cfengine3();
		"any" usebundle => base_networking();
		"any" usebundle => base_apt();

}

bundle agent base_cfengine3
{
methods:

		"any" usebundle => insert_template(
			"/etc/default/cfengine3", "cfengine3_default");

	insert_template__etc_default_cfengine3_repaired::
		"any" usebundle => service("cfengine3", "restart");

}

bundle agent base_apt
{
methods:
		"any" usebundle => insert_template(
			"/etc/apt/sources.list", "apt_sources.list");
}

bundle agent base_networking
{
classes:
		"am_gateway"
			expression => strcmp("$(base_config.net_gateway)","");
		"package_dnsmasq" expression => fileexists(
			"/usr/sbin/dnsmasq");

packages:
	am_gateway::
		"dnsmasq"
			package_policy => "addupdate",
			package_method => generic;

	package_dnsmasq.!am_gateway::
		"dnsmasq"
			package_policy => "delete",
			package_method => generic;
			
processes:
	am_gateway.package_dnsmasq::
		"/usr/sbin/dnsmasq"
			restart_class => "dnsmasq_not_running";

methods:
		"any" usebundle => insert_template(
			"/etc/network/interfaces", "net_interfaces" );

		"any" usebundle => insert_template(
			"/etc/hostname", "net_hostname" );

		"any" usebundle => insert_template(
			"/etc/mailname", "net_mailname");

		"any" usebundle => insert_template(
			"/etc/hosts", "net_hosts");

		"any" usebundle => insert_template(
			"/etc/resolv.conf", "net_resolv_conf");
	am_gateway::
		"any" usebundle => insert_template(
			"/etc/network/firewall", "net_firewall" );

		"any" usebundle => insert_template(
			"/etc/sysctl.d/ip_forward", "sysctl_d_ip_forward" );

	am_gateway.package_dnsmasq::

		"any" usebundle => insert_template(
			"/etc/dnsmasq.conf", "net_dnsmasq_conf");

	insert_template__etc_network_interfaces_repaired|insert_template__etc_network_firewall_repaired::

		"any" usebundle => restart_interface("$(base_config.net_dev)");

	am_gateway.insert_template__etc_dnsmasq_conf_repaired::

		"any" usebundle => service("dnsmasq", "reload");

	am_gateway.package_dnsmasq.dnsmasq_not_running::

		"any" usebundle => service("dnsmasq", "start");

commands:

	am_gateway.insert_template__etc_sysctl_d_ip_forward_repaired::

		"/sbin/sysctl -p /etc/sysctl.d/ip_forward";

}
